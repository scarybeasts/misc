MODE7:HIMEM=&4A00:VDU129,157,131:PRINT"Disc BEAST v0.4":PRINT
D%=&4A00:Z%=&60:U%=&5200:W%=&50:DIM V%(8),G%(9):DIM C% 11
F$="DISC":G%(6)=1:G%(7)=1

REPEAT
INPUT A$:IF LEN(A$)<4 THEN A$="????"
P$=MID$(A$,5):A$=LEFT$(A$,4)
FOR I%=0 TO 8:V%(I%)=-1:NEXT
I%=0
REPEAT
J%=INSTR(P$," "):IF J%=0 THEN J%=LEN(P$)
Q$=LEFT$(P$,J%)
IF LEN(Q$)<>0 AND Q$<>" " AND Q$<"A" THEN V%(I%)=EVAL(Q$):I%=I%+1
P$=RIGHT$(P$,LEN(P$)-J%)
UNTIL LEN(P$)=0
P%=I%
REM Bufs, &5400-&7BFF
B%=&5C00:PROCbufs(B%,B%+4096)
IF A$="INIT" THEN PROCini:A$=""
IF A$="DUMP" THEN PROCd(V%(0)):A$=""
IF A$="BFIL" THEN PROCbfil:A$=""
IF A$="BSET" THEN PROCbset:A$=""
IF A$="SEEK" THEN T%=V%(0):PROCs:A$=""
IF A$="RIDS" THEN PROCclr:PROCrids:PROCr:PRINT"HEADERS: "+STR$(S%):PROCd(0):A$=""
IF A$="READ" THEN PROCclr:PROCread:PROCr:PROCdtim:L%=FNl(Z%)-B%:!C%=-1:PROCc32(B%,L%,C%):PROCc32f(C%):PRINT"CRC32 "+STR$(L%)+" BYTES: "+STR$~(!C%):PROCd(0):A$=""
IF A$="RTRK" THEN PROCclr:PROCrtrk:PROCr:PRINT"LEN: "+STR$(S%):PROCd(0):A$=""
IF A$="WRIT" THEN PROCwrit:PROCr:A$=""
IF A$="WTRK" THEN PROCwtrk:PROCr:A$=""
IF A$="TIME" THEN CALL D%+21:PRINT"DRIVE SPEED: "+STR$(FNl(Z%+4)):A$=""
IF A$="DTRK" THEN PROCdtrk:A$=""
IF A$="DCRC" THEN PROCdcrc:A$=""
IF A$="HFEG" THEN PROChfeg:A$=""
IF A$="HFEP" THEN PROChfep:A$=""
IF A$="STRT" THEN PROCg(3)
IF A$="BAIL" THEN PROCg(4)
IF A$="DSTP" THEN PROCg(5)
IF A$="FDRV" THEN PROCg(6)
IF A$="AUTO" THEN PROCg(7)
IF A$="DR40" THEN PROCg(8)
IF A$="CRCO" THEN PROCg(9)
IF A$="BFUN" THEN ?(Z%+10)=V%(0):A$=""
IF A$="FSYS" THEN F$=Q$:G%(6)=-1:PRINT"FSYS "+F$:A$=""
IF A$="CMFM" THEN PROCcmfm:A$=""
IF A$="SCRC" THEN PROCscrc:A$=""
IF A$="FCRC" THEN PROCfcrc:A$=""
IF A$="OCLI" THEN OSCLI(Q$):A$=""
IF A$<>"" THEN PRINT "???"
UNTIL FALSE

DEF PROCg(A%):G%(A%)=V%(0):PRINT A$;:PRINT V%(0):A$="":ENDPROC

DEF PROCini
A%=V%(0):T%=0:IF A%<0 THEN A%=0
G%(0)=A%:A%=USR(D%) AND &FF
IF A%=0 THEN PRINT"FAIL":END
IF A%=1 THEN PRINT"OK 8271"
IF A%=2 THEN PRINT"OK 1770"
G%(1)=A%
PRINT"DRIVE "+STR$(G%(0))+" SPEED: "+STR$(FNl(Z%+4))
PRINT"ENSURE DRIVE IN 80 TRACK MODE"
ENDPROC

DEF PROCclr:PROCstor(B%,0,4096):ENDPROC

DEF PROCbfil
A%=0:X%=0
IF V%(1)<>-1 THEN A%=V%(0):X%=V%(1) ELSE X%=V%(0)
Y%=4096-A%:PROCstor(B%+A%,X%,Y%)
ENDPROC

DEF PROCbset
A%=V%(0)
FOR I%=1 TO 8
IF V%(I%)<>-1 THEN ?(B%+A%+I%-1)=V%(I%)
NEXT
ENDPROC

DEF PROCr:I%=R% AND &FF:J%=(R% AND &FF00) DIV 256:PRINT"RESULT: &"+STR$~(I%)+" (&"+STR$~(J%)+")":ENDPROC

DEF PROCdtim:A%=V%(3)/128:X%=FNl(B%+4096):Y%=FNl(B%+4096+A%*2):PRINT"TIME: "+STR$(Y%-X%):ENDPROC

DEF PROCstor(A%,X%,Y%):PROCp(W%,A%):Y%=-Y%:PROCp(W%+4,Y%):A%=X%:CALL U%:ENDPROC

DEF PROCc(A%,X%,Y%):PROCp(W%,A%):PROCp(W%+2,X%):Y%=-Y%:PROCp(W%+4,Y%):CALL U%+3:ENDPROC

DEF PROCmfm(A%,X%,Y%):PROCp(W%,A%):PROCp(W%+2,X%):Y%=-Y%:PROCp(W%+4,Y%):CALL U%+15:ENDPROC

DEF PROCcmp(A%,X%,Y%):PROCp(W%,A%):PROCp(W%+2,X%):Y%=-Y%:PROCp(W%+4,Y%):R%=USR(U%+12) AND &FF:ENDPROC

DEF PROCcrc16(A%,X%):PROCp(W%+2,A%):X%=-X%:PROCp(W%+4,X%):X%=&FF:Y%=&FF:R%=USR(U%+6):X%=(R% AND &FF00) DIV &100:Y%=(R% AND &FF0000) DIV &10000:R%=X%*256+Y%:ENDPROC

DEF PROCc32(A%,X%,Y%):PROCp(W%,Y%):PROCp(W%+2,A%):X%=-X%:PROCp(W%+4,X%):CALL U%+9:ENDPROC

DEF PROCc32f(A%):!(C%+8)=!A% EOR &FFFFFFFF:?A%=?(C%+11):?(A%+1)=?(C%+10):?(A%+2)=?(C%+9):?(A%+3)=?(C%+8):ENDPROC

DEF PROCbufs(A%,Y%):PROCp(Z%,A%):PROCp(Z%+2,Y%):PROCp(Z%+11,&7000):PROCp(Z%+6,G%(3)):PROCp(Z%+8,-G%(4)):ENDPROC

DEF PROCs
A%=T%:IF A%=-1 THEN A%=0
IF G%(5) THEN A%=A%*2
CALL D%+9
ENDPROC

DEF PROCrids
K%=FNl(Z%+2):R%=USR(D%+15):S%=0
IF (R% AND &FF)=&18 THEN ENDPROC
J%=FNl(Z%+4)
FOR I%=0 TO 31
L%=FNl(K%+I%*2)
IF L%>0 AND L%<J% THEN S%=S%+1 ELSE I%=31
NEXT
ENDPROC

DEF PROCrw(I%)
IF V%(0)=-1 THEN V%(0)=T%
IF V%(1)=-1 THEN V%(1)=0
IF V%(2)=-1 THEN V%(2)=1
IF V%(3)=-1 THEN V%(3)=256
X%=V%(1):Y%=V%(2):A%=V%(3)
IF A%=256 THEN Y%=Y%+&20
IF A%=512 THEN Y%=Y%+&40
IF A%=1024 THEN Y%=Y%+&60
IF A%=2048 THEN Y%=Y%+&80
IF A%=4096 THEN Y%=Y%+&A0
A%=V%(0):R%=USR(D%+I%)
ENDPROC

DEF PROCread:PROCrw(18):ENDPROC

DEF PROCwrit:PROCrw(24):ENDPROC

DEF PROCrtrk
IF G%(1)<>2 THEN PRINT"1770 ONLY":ENDPROC
S%=FNl(Z%):A%=V%(0):R%=USR(D%+12):S%=FNl(Z%)-S%
ENDPROC

DEF PROCwtrk
IF G%(1)<>2 THEN PRINT"1770 ONLY":ENDPROC
A%=V%(0):R%=USR(D%+27)
ENDPROC

DEF PROCcmfm:PROCc(B%+4096,B%,4096):PROCmfm(B%,B%+4096,4096):ENDPROC

DEF PROCd(A%)
A$="":IF A%<0 THEN A%=0
FOR I%=0 TO 63
X%=?(B%+A%+I%):PRINT" "+FNhex(X%);:IF X%<32 OR X%>126 THEN A$=A$+"." ELSE A$=A$+CHR$(X%)
IF I% MOD 8=7 THEN PRINT"  "+A$:A$=""
NEXT
ENDPROC

DEF PROCdtrk
PROCtrk:J%=?(B%+5)
PRINT"TRACK "+STR$(T%)+" "+STR$(J%)+" SECTORS, LEN "+STR$(FNl(B%+6))
IF R%=2 THEN PRINT"FAIL"
IF J%=0 THEN ENDPROC
FOR I%=0 TO J%-1
IF FNcrcerr(I%) THEN VDU129:PRINT"SECTOR "+STR$(I%)+" CRC ERROR"
IF FNsizem(I%) THEN VDU131:PRINT"SECTOR "+STR$(I%)+" SIZE MISMATCH"
IF FNidtrk(I%)<>T% THEN VDU134:PRINT"SECTOR "+STR$(I%)+" TRACK MISMATCH"
NEXT
PRINT "CRC32 "+STR$~(!C%)
ENDPROC

DEF PROCscrc:I%=V%(0):J%=V%(1):!C%=-1:V%(2)=1:V%(3)=256
REPEAT:T%=I%/10:PROCs:V%(0)=T%:V%(1)=I% MOD 10:PROCbufs(B%,B%+256):PROCread:R%=R% AND &FF:IF R%<>0 THEN PROCr
K%=J%:IF K%>256 THEN K%=256
PROCc32(B%,K%,C%):I%=I%+1:J%=J%-K%:UNTIL J%=0
PROCc32f(C%):PRINT"CRC32 "+STR$~(!C%)
ENDPROC

DEF PROCfcrc
T%=0:PROCs:V%(0)=0:V%(1)=0:V%(2)=2:V%(3)=256:PROCbufs(B%+512,B%+1024):PROCread:R%=R% AND &FF:IF R%<>0 THEN PROCr
L%=?(B%+&305)/8:IF L%=0 THEN ENDPROC
FOR M%=0 TO L%-1
J%=B%+&208+M%*8
FOR I%=0 TO 6:K%=?(J%+I%):IF K%<32 OR K%>126 THEN VDU46 ELSE VDU K%
NEXT
I%=B%+&30C+M%*8:V%(0)=?(I%+3)+(?(I%+2) AND 3)*256:V%(1)=FNl(I%)
PRINT"  "+STR$~(V%(0))+" "+STR$~(V%(1))+" ";:PROCscrc
NEXT
ENDPROC

DEF PROCtcrc
!C%=0
IF G%(9) AND 1 THEN ENDPROC
J%=?(B%+5):IF J%=0 THEN ENDPROC
!C%=-1
FOR I%=0 TO J%-1
VDU8,48+I%:K%=1:IF FNcrcerr(I%) THEN K%=0
L%=FNidtrk(I%):IF L%=&FF OR (L%=0 AND T%<>0) THEN K%=0
L%=FNrsiz(I%)+1:M%=FNsaddr(I%):N%=?M%:?M%=N% OR &F0
IF K%=1 THEN PROCc32(M%,L%,C%)
?M%=N%
NEXT
PROCc32f(C%)
ENDPROC

DEF PROCtrk:IF T% AND 1 THEN B%=&6C00 ELSE B%=&5C00
VDU135,46:PROCclr:VDU8,83:PROCs:PROCgtrk:PROCtcrc:!(B%+12)=!C%:VDU8,8
ENDPROC

DEF PROCcrcp:VDU130:PRINT "DISC CRC32 "+STR$~(!(C%+4))
IF (T%=41 OR T%=81) AND !C%<>0 THEN PRINT"WARN: PAST END TRACK"
ENDPROC

DEF PROCchks
A%=G%(5):I%=80:IF G%(8) THEN I%=40
PRINT"(DRIVE TYPE "+STR$(I%)+", DOUBLE STEP: "+STR$(A%)+")"
IF A% AND V%(7)>40 THEN PRINT"ERR: >40 TRACKS AND DOUBLE STEP"
ENDPROC

DEF PROCauto
V%(6)=V%(0):IF V%(0)=-1 THEN V%(6)=0
V%(7)=V%(1):IF V%(1)=-1 THEN V%(7)=40
IF G%(7)=0 OR G%(8) THEN ENDPROC
IF G%(1)<>2 THEN ENDPROC
PRINT"WARN: DRIVE MUST BE 80 TRACK":G%(5)=0
FOR T%=1 TO 21 STEP 20
PROCs:PROCbufs(B%,B%+4096):PROCrids:R%=R% AND &FF:PRINT"TRACK "+STR$(T%)+" READ IDS: &"+STR$~(R%)
IF R%=0 THEN T%=100
NEXT
IF T%=41 THEN G%(5)=1
I%=80-G%(5)*40:PRINT"AUTO TRACKS: "+STR$(I%)
IF V%(1)=-1 THEN V%(7)=I%
ENDPROC

DEF PROCdcrc
PROCauto:PROCchks:!(C%+4)=-1
FOR T%=V%(6) TO V%(7)
IF (T% AND 3)=0 THEN PRINT:PRINT STR$(T%);
PROCtry
IF R%=2 THEN T%=V%(7) ELSE PRINT" "+STR$~(!C%);
NEXT
PRINT:IF R%=2 THEN PRINT"FAIL" ELSE PROCc32f(C%+4):PROCcrcp
ENDPROC

DEF PROCtry
V%(4)=5
REPEAT:V%(4)=V%(4)-1:PROCtrk
S%=0:IF R%=1 OR R%=2 THEN VDU7,129,33,8,8:IF V%(4)<>0 THEN PROCw(100):T%=T%-1:PROCs:T%=T%+1:PROCs:S%=1
UNTIL S%=0 OR V%(4)=0
IF (G%(9) AND 1)=0 THEN PROCc32(C%,4,C%+4)
ENDPROC

DEF PROChfeg
PROCauto:PROCchks
PRINT:VDU132,157,134:PRINT"HFE Grab v0.3"
!(C%+4)=-1:V%(5)=V%(2)
PRINT"TRACKS "+STR$(V%(6))+" TO "+STR$(V%(7))
IF V%(5)=0 THEN PRINT"(NOT SAVING)"
FOR T%=V%(6) TO V%(7)
IF (T% AND 1)=0 THEN PROCstor(&5C00,0,8192)
IF (T% MOD 10)=0 THEN PRINT:PRINT STR$(T%)+" ";
IF T%=0 THEN VDU32
PROCtry
IF R%<>2 AND T%=V%(7) THEN PROCc32f(C%+4):!(B%+28)=!(C%+4)
IF R%<>2 THEN PROChfegy ELSE T%=V%(7)
NEXT
PRINT:IF R%=2 THEN PRINT"FAIL" ELSE PROCcrcp
ENDPROC

DEF PROChfegy
I%=2:J%=32:K%=255:A%=?(B%+5)
IF A%<10 THEN J%=48+A%
IF A%>10 THEN J%=43
IF ?(B%+4)=&18 THEN I%=4:J%=33
IF A%>0 THEN PROChfegs
VDU128+I%,K%,J%:PROCsave:A%=G%(0):CALL D%+3
ENDPROC

DEF PROChfegs:Y%=0:L%=0
FOR X%=0 TO A%-1
IF FNidtrk(X%)<>T% THEN I%=7
IF FNidtrk(X%)=0 AND T%<>0 THEN I%=6
IF FNsizem(X%) THEN I%=3
IF FNcrcerr(X%) THEN L%=1
IF (?FNsaddr(X%) AND &F)=&8 THEN K%=68
Y%=Y%+FNrsiz(X%)
NEXT
IF Y%>2560 THEN I%=5
IF L%=1 THEN I%=1
ENDPROC

DEF PROCsave
IF V%(5)=0 THEN ENDPROC
IF F$="DRV" THEN PROCdrv:ENDPROC
IF (T% AND 1)=0 AND T%<>V%(7) THEN ENDPROC
PROCfsel:OSCLI("SAVE TRKS"+STR$(T% AND &FE)+" 5C00 +2000")
ENDPROC

DEF PROCdrv:?(Z%+13)=T%-1:A%=G%(6):CALL D%+3:PROCwmfm:ENDPROC

DEF PROCfsel
OSCLI(F$):A%=G%(6):IF T%>40 THEN A%=A%+2
IF G%(6)>-1 THEN OSCLI("DR."+STR$(A%))
ENDPROC

DEF PROChfep
V%(6)=V%(0):IF V%(0)=-1 THEN V%(6)=0
V%(7)=V%(1):IF V%(1)=-1 THEN V%(7)=40
PRINT"HFE PUT TRACKS "+STR$(V%(6))+" TO "+STR$(V%(7))
FOR T%=V%(6) TO V%(7)
VDU46:PROCfsel:OSCLI("LOAD TRKS"+STR$(T% AND &FE)+" 5C00"):A%=G%(0):CALL D%+3
PROCwmfm
NEXT
ENDPROC

DEF PROCwmfm:IF T% AND 1 THEN PROCc(&5C00,&6C00,4096):B%=&5C00
M%=&5E00:PROCc(&5B00,&6B00,256):PROCc(&6E00,M%,3328):PROCmfm(M%,&6E00,3328)
J%=?(B%+5):IF J%>0 THEN PROChfef
PROCp(W%,M%):PROCp(W%+4,-6656):CALL U%+18
REM Long tracks
N%=0:IF FNl(B%+6)>3145 THEN REPEAT:N%=N%+1:UNTIL ?(M%+N%)=&AA
PROCs:PROCbufs(M%+N%,&5400):V%(0)=1:PROCwtrk:R%=R% AND &FF
IF R%<>0 THEN PROCr
ENDPROC

DEF PROChfef
FOR I%=0 TO J%-1
PROChfet(FNl(B%+&100+I%*2)):PROChfet(FNl(B%+&140+I%*2))
REM Weak bits
K%=FNl(&5B00+I%*2):IF K%>0 THEN K%=FNsaddr(I%)+1+K%:K%=(K%-M%)*2:K%=K%+M%:FOR L%=0 TO 32:?(K%+L%)=&55+L%:NEXT
NEXT
ENDPROC

DEF PROChfet(A%)
A%=A%*2+M%:?A%=&F5:A%=A%+1
IF ?A%=&FE THEN ?A%=&7E
IF ?A%=&EF THEN ?A%=&6F
IF ?A%=&EA THEN ?A%=&6A
IF !(A%-5)=&AAAAAAAA THEN ENDPROC
FOR K%=2 TO 13:?(A%-K%)=&AA:NEXT
ENDPROC

DEF PROCgtrk
?B%=G%(1):?(B%+1)=T%:?(B%+2)=?(Z%+4):?(B%+3)=?(Z%+5):?(B%+&10)=3
PROCbufs(B%+&20,B%+&A0):VDU8,73:PROCrids
REM 1770 ghosts
IF S%=0 THEN R%=&18
R%=R% AND &FF:?(B%+4)=R%:?(B%+5)=S%
IF R%=&18 THEN R%=0:ENDPROC

IF R%<>0 THEN ?(B%+8)=1
IF R%<>0 AND G%(1)=1 THEN R%=1:ENDPROC
VDU8,82:IF G%(1)=1 THEN PROCg8271 ELSE PROCg1770
IF R%<>0 THEN ENDPROC

REM Timing based sector sizes
J%=?(B%+5):O%=FNcstime(0)
FOR I%=0 TO J%-1
L%=-24-O%
IF I%=J%-1 THEN L%=L%+FNl(B%+6) ELSE O%=FNcstime(I%+1):L%=L%+O%
A%=4
IF L%<2048 THEN A%=3
IF L%<1024 THEN A%=2
IF L%<512 THEN A%=1
IF L%<256 THEN A%=0
?(B%+&E0+I%)=A%
NEXT

REM Parse sectors
O%=0
FOR I%=0 TO J%-1
VDU8,48+I%
IF I%=0 THEN M%=-1 ELSE M%=FNl(B%+&100+(I%-1)*2)
M%=M%-O%:O%=FNcstime(I%):M%=M%+O%
P%=!(B%+&20+I%*4):N%=0:L%=B%+&200+M%
FOR K%=0 TO 20
IF K% AND 1 THEN L%=L%+K% ELSE L%=L%-K%
IF (?L%=&FE OR ?L%=&CE) AND !(L%+1)=P% THEN K%=100
NEXT
IF K%=101 THEN N%=1:M%=L%-B%-&200:PROCp(B%+&100+I%*2,M%)
L%=B%+&200+M%+14
FOR K%=0 TO 16
IF ?L%=&FB OR ?L%=&CB OR ?L%=&F8 OR ?L%=&C8 THEN K%=100 ELSE L%=L%+1
NEXT
IF K%=101 THEN N%=N%+1:M%=L%-B%-&200:PROCp(B%+&140+I%*2,M%)
IF N%=2 THEN PROCgtrky:R%=0 ELSE R%=2:I%=J%
NEXT
IF R%=0 AND ?(B%+8)=1 THEN R%=1
REM 1770 ghosts
IF R%=2 AND ?(B%+4)<>0 THEN ?(B%+4)=&18:?(B%+5)=0:R%=3:ENDPROC
IF R%<>1 THEN ENDPROC
REM Weak bits
FOR I%=0 TO J%-1
IF FNcrcerr(I%) THEN PROCweak
NEXT
R%=1
ENDPROC

DEF PROCweak
PROCrsec(I%)
IF R%<>0 AND R%<>&E THEN ENDPROC
A%=FNrsiz(I%):X%=FNssize(FNidsiz(I%))
IF X%<A% THEN A%=X%
PROCcmp(&5400,FNsaddr(I%)+1,A%)
A%=B%+&E0+I%:X%=FNl(W%)-&5400
IF R%<>0 THEN ?A%=?A%+&20:PROCp(B%+&F00+I%*2,X%)
ENDPROC

DEF PROCrsec(A%)
K%=0:IF A%>0 THEN K%=FNstime(A%)
PROCbufs(&5400,&100):PROCp(Z%+6,K%)
V%(0)=FNidtrk(A%):V%(1)=FNidsec(A%):V%(2)=1:V%(3)=FNssize(FNidsiz(A%))
PROCread:R%=R% AND &FF
ENDPROC

DEF PROCgtrky
IF G%(9) AND 2 THEN ENDPROC
K%=B%+&E0+I%:M%=?K%+1
REM Try other sizes if CRC16 fails
REPEAT:M%=M%-1
A%=FNssize(M%)+1
N%=?L%:?L%=N% OR &F0:PROCcrc16(L%,A%):?L%=N%
S%=?(L%+A%)*256+?(L%+A%+1)
UNTIL R%=S% OR M%=0
REM Tag mismatches
IF R%=S% THEN ?K%=M% ELSE M%=?K%:?K%=(?K%)+&80:?(B%+8)=1
IF M%<>FNidsiz(I%) THEN ?K%=(?K%)+&40
ENDPROC

DEF PROCg1770
FOR I%=1 TO 32
FOR J%=1 TO 4
PROCbufs(B%+&200,B%+&180):V%(0)=0:PROCrtrk:R%=R% AND &FF
IF R%<>0 THEN PROCr
IF R%=0 THEN J%=4
NEXT
K%=0
FOR J%=&202 TO &240
IF ?(B%+J%)=&CE THEN K%=1:J%=&240
IF ?(B%+J%)=&FE AND ?(B%+J%-2)=0 THEN J%=&240
NEXT
IF K%=0 THEN I%=32
NEXT
R%=0:PROCp(B%+6,S%)
ENDPROC

DEF PROCg8271
PROCstor(B%+&200,&FF,3125):PROCstor(&5400,0,2048):PROCp(B%+6,3125):J%=?(B%+5):K%=0
FOR I%=0 TO J%-1
REM Header
M%=B%+&200+FNcstime(I%)-1
?M%=&FE:!(M%+1)=!(B%+&20+I%*4)
PROCcrc16(M%,5):?(M%+5)=(R% AND &FF00) DIV 256:?(M%+6)=R%
M%=M%+7+17
PROCbufs(&5400,&100)
PROCp(Z%+6,K%):K%=FNstime(I%)
V%(0)=FNidtrk(I%):V%(1)=FNidsec(I%):V%(2)=1:V%(3)=2048
R%=0:IF V%(0)<>&FF AND (T%=0 OR V%(0)<>0) THEN PROCread:R%=R% AND &FF
IF R%=&18 THEN PRINT"ERR: "+STR$~(V%(0))+" "+STR$~(V%(1)):END
REM Data
IF R% AND &20 THEN ?M%=&F8 ELSE ?M%=&FB
M%=M%+1
IF I%=J%-1 THEN L%=3328 ELSE L%=FNcstime(I%+1)
L%=L%-(M%-B%-&200):PROCc(M%,&5400,L%)
NEXT:R%=0
ENDPROC

DEF PROCw(A%):X%=TIME:REPEAT:UNTIL TIME>X%+A%:ENDPROC

DEF FNhex(A%):P$=STR$~(A%):IF LEN(P$)=1 THEN P$="0"+P$
=P$

DEF FNl(A%):=?A%+?(A%+1)*256
DEF PROCp(A%,X%):?A%=X%:?(A%+1)=(X% AND &FF00) DIV 256:ENDPROC

DEF FNssize(A%)
A%=A% AND 7
IF A%=1 THEN =256
IF A%=2 THEN =512
IF A%=3 THEN =1024
IF A%=4 THEN =2048
=128
DEF FNcrcerr(A%):=?(B%+&E0+A%) AND &80
DEF FNsizem(A%):=?(B%+&E0+A%) AND &40
DEF FNidtrk(A%):=?(B%+&20+A%*4)
DEF FNidsec(A%):=?(B%+&20+A%*4+2)
DEF FNidsiz(A%):=?(B%+&20+A%*4+3)
DEF FNrsiz(A%):A%=?(B%+&E0+A%):=FNssize(A%)
DEF FNsaddr(A%):=B%+&200+FNl(B%+&140+A%*2)
DEF FNstime(A%):=FNl(B%+&A0+A%*2)

DEF FNcstime(A%):A%=FNstime(A%):=A%*(FNl(B%+6)/FNl(Z%+4))
